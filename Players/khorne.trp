{
Program: Khorne

Author: Denahiro

Description: Adaptation of the Rambo.trp to be even more stupid
}

procedure Assignment(var ToTerritory: integer);
var
  R: double;
  T: integer;
begin
  // find random free territory (a very stupid way to start the game!)
  ToTerritory:=0;
  repeat
    R:=URandom(42);
    T:=round(R);
    if TOwner(T)=0 then
      ToTerritory:=T;
  until (ToTerritory>0);
end;

procedure Placement(var ToTerritory: integer);
var
  T,Armies,MaxArmies: integer;
begin
  // reinforce territory with most armies and front
  ToTerritory:=0;
  MaxArmies:=0;
  for T:=1 to 42 do begin
    if TIsFront(T) then begin  // territory is mine and front
      Armies := TArmies(T);
      if Armies>MaxArmies then begin
        ToTerritory:=T;
        MaxArmies:=Armies;
      end;
    end;
  end;
end;

procedure Attack(var FromTerritory, ToTerritory: integer);
var
  T,EnemyT,EnemyA: integer;
begin
  // attack weakest point until exhausted
  FromTerritory:=0;
  ToTerritory:=0;
  for T:=1 to 42 do begin
    if TIsFront(T)                      // territory is mine and front
    and (TArmies(T)>1) then begin       // attack is possible
      TWeakestFront(T,EnemyT,EnemyA);
      FromTerritory:=T;
      ToTerritory:=EnemyT;
    end;
  end;
end;

procedure Occupation(FromTerritory, ToTerritory: integer; var Armies: integer);
begin
  //Move all
  Armies:=TArmies(FromTerritory)-1;
end;

procedure Fortification(var FromTerritory, ToTerritory, Armies: integer);
var
  T,B,MaxArmy: integer;
begin
  FromTerritory:=0;
  ToTerritory:=0;
  Armies:=0;

  // look for the greatest nuber of armies which is not on a front territory
  MaxArmy := 1;
  for T:=1 to 42 do begin
    if TIsMine(T) and not TIsFront(T) then begin  // territory is mine and not "front"
      if TArmies(T)>MaxArmy then begin
        MaxArmy := TArmies(T);
        FromTerritory := T;
      end;
    end;
  end;
  // if there are armies to move...
  if FromTerritory>0 then begin
    // check if can move armies to front...
    for B:=1 to TBordersCount(FromTerritory) do begin
      if ToTerritory=0 then begin
        T:=TBorder(FromTerritory,B);
        if TIsMine(T) and TIsFront(T) then begin
          ToTerritory:=T;
        end;
      end;
    end;
    // otherwise move armies in the first bordering territory
    if ToTerritory=0 then begin
      for B:=1 to TBordersCount(FromTerritory) do begin
        if ToTerritory=0 then begin
          T:=TBorder(FromTerritory,B);
          if TIsMine(T) then begin
            ToTerritory:=T;
          end;
        end;
      end;
    end;
    // Move all armies to destination (except the one which must stay)
    if ToTerritory>0 then begin
      Armies:=TArmies(FromTerritory)-1;
    end;
  end;
end;

begin
end.
